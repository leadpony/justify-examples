allprojects {
    group = 'org.leadpony.justify'
    version = '2.1.0'
}

subprojects {
    apply plugin: 'java'

    compileJava {
        doFirst {
            options.encoding = 'UTF-8'

            if (JavaVersion.current().isJava9Compatible()) {
                options.compilerArgs = [
                    '--module-path', classpath.asPath
                ]
                classpath = files()
            } else {
                exclude 'module-info.java'
            }
        }
    }

    task copyDependencies(type: Copy) {
        from configurations.default
        into "${buildDir}/libs"
    }

    task copyScripts(type: Copy) {
        from 'src/main/scripts', 'src/main/json'
        into "${buildDir}"
    }

    build.dependsOn copyDependencies, copyScripts

    dependencies {
        implementation 'org.leadpony.justify:justify:2.1.0'
        runtimeOnly 'org.glassfish:jakarta.json:1.1.6:module'
    }

    repositories {
        mavenCentral()
    }
}
