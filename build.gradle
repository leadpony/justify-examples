allprojects {
    group = 'org.leadpony.justify'
    version = '3.1.0-SNAPSHOT'
}

subprojects {

    apply plugin: 'java'

    compileJava {
        doFirst {
            options.encoding = 'UTF-8'
            options.compilerArgs = [
                '--module-path', classpath.asPath
            ]
            classpath = files()
        }
    }

    task copyDependencies(type: Copy) {
        from configurations.runtimeClasspath
        into "${buildDir}/libs"
    }

    task copyScripts(type: Copy) {
        from 'src/main/scripts', 'src/main/json', 'src/main/yaml'
        into "${buildDir}"
    }

    build.dependsOn copyDependencies, copyScripts

    dependencies {
        constraints {
            implementation 'org.leadpony.justify:justify:3.1.0'
            implementation 'jakarta.json.bind:jakarta.json.bind-api:2.0.0'
            runtimeOnly 'org.glassfish:jakarta.json:2.0.0'
            runtimeOnly 'org.eclipse:yasson:2.0.0-M2'
            runtimeOnly 'org.leadpony.joy:joy-yaml:2.1.0'
        }
    }

    repositories {
        mavenCentral()
    }
}
